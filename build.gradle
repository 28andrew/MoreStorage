import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.2'
    id 'java'
}

group 'me.andrew28'
version '0.1.1'

def spigotVersion = '1.12.2-R0.1-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    // Spigot Repository
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    // For the 'bungeecord-chat' transitive dependency of Spigot
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    // bstats
    maven {
        url 'http://repo.bstats.org/content/repositories/releases/'
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile group: 'org.spigotmc', name: 'spigot', version: spigotVersion
    compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'org.bstats', name: 'bstats-bukkit', version: '1.2'
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

shadowJar {
    dependencies {
        include(dependency('org.bstats:bstats-bukkit'))
    }
    relocate 'org.bstats', 'me.andrew28.morestorage.org.bstats'
}

processResources {
    filter ReplaceTokens, tokens: [
        "version": project.property("version")
    ]
}

jar.enabled = false
build.dependsOn(shadowJar)